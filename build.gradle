
defaultTasks 'clean', 'build', 'dist'

apply plugin: 'java'
apply plugin: 'application'
mainClassName = "fetchmailatt.FetchMailAtt"

version = '1.0'


repositories {
    mavenCentral()
}

dependencies {
    compile 'javax.mail:mail:1.4.7'
    testCompile 'junit:junit:4.+'
}

jar {
    manifest {
        attributes 'Main-Class': 'fetchmailatt.FetchMailAtt'
    }
}

distZip {
    archiveName "$baseName-${version}.zip"
}

applicationDistribution.from("conf/") {
//  into "bin"
    into "conf"
}

startScripts {
    // Add lib/conf to start script's classpath.  It forces adding a 'lib' in front.
    classpath += files("conf")

    doLast {
        // Remove the lib/ in front of conf.
        def windowsScript = file getWindowsScript()
        def unixScript    = file getUnixScript()
        windowsScript.text = windowsScript.text.replace("lib\\conf", "conf")
        unixScript.text  = unixScript.text.replace("lib/conf", "conf")
    }
}

run {
    classpath.add(files("${projectDir}/conf"))
    systemProperty 'logLevel', System.getProperty('logLevel')   // pass in as gradle run -DlogLevel=FINER
    if (System.getProperty("args") != null) {
        args System.getProperty("args").split()                 // pass in as gradle run -Dargs="arg1 arg2 ..."
    }
}

test {
    testLogging.showStandardStreams = true
}

